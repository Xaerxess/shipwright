#!/usr/bin/env perl
use warnings;
use strict;

use File::Spec;
use File::Temp qw/tempdir/;
use File::Copy qw/move copy/;
use File::Find qw/find/;
use Config;
use Getopt::Long;
use Cwd;
use inc::YAML::Tiny;

my $build_base = getcwd;

my %args;
GetOptions(
    \%args,    'install-base=s', 'perl=s',    'skip=s',
    'flags=s', 'skip-test',      'only-test', 'force',
    'clean',   'name=s',         'help',
);

my $USAGE = <<'END'
run: ./bin/shipwright-builder

options: 

help: print this usage

install-base: where we will install
    defaults: a temp dir below your system's tmp.
    e.g. --install-base /home/local/mydist

name: the name of the project. used to create a better named dir if
    install_base is not supplied
    e.g. --name mydist

perl: which perl to use for the to be installed dists. 
    defaults: if we have perl in the source, it will use that one.
              else, it will use the one which runs this builder script.
    e.g. --perl /usr/bin/perl

skip: dists we don't want to install, comma seperated. 
    e.g. --skip perl,Module-Build

flags: set flags we need, comma seperated
   e.g.  --flags mysql,standalone

skip-test: skip test part if there're

force: if tests fail, install anyway

only-test: test for the installed dists.
    it's used to be sure everything is ok after we install with success. 
    need to specify --install-base if nothing find in __install_base.

clean: clean the source

END
  ;

if ( $args{'help'} ) {
    print $USAGE;
    exit 0;
}

$args{skip} = { map { $_ => 1 } split /\s*,\s*/, $args{skip} || '' };
$args{flags} = {
    default => 1,
    map { $_ => 1 } split /\s*,\s*/, $args{flags} || ''
};

# YAML::Tiny objects are array based.
my $order =
  ( YAML::Tiny->read( File::Spec->catfile( 'shipwright', 'order.yml' ) ) )->[0];
my $flags =
  ( YAML::Tiny->read( File::Spec->catfile( 'shipwright', 'flags.yml' ) ) )->[0];

my $log;

if ( $args{'only-test'} ) {
    open $log, '>', 'test.log' or die $!;

    $args{'install-base'} = get_install_base() unless $args{'install-base'};
    test();
}
elsif ( $args{'clean'} ) {
    open $log, '>', 'clean.log' or die $!;

    $args{'install-base'} = get_install_base() unless $args{'install-base'};

    for my $dist (@$order) {
        next
          if $flags->{$dist} && !grep { $args{flags}{$_} } @{ $flags->{$dist} };

        unless ( $args{skip}{$dist} ) {
            clean($dist);
        }
        chdir $build_base;
    }
}
else {

    # for install
    open $log, '>', 'build.log' or die $!;

    require CPAN;

    # don't bother people no CPAN::Config since it's not a problem
    eval { require CPAN::Config };

    # we don't want any prereqs any more!
    {
        no warnings 'once';
        $CPAN::Config->{prerequisites_policy} = 'ignore';
    }

    unless ( $args{name} ) {
        if ( $build_base =~ m{([-.\w]+)[\\/]([.\d]+)$} ) {
            $args{name} = "$1-$2";
        }
        elsif ( $build_base =~ m{([-.\w]+)$} ) {
            $args{name} = $1;
        }
    }

    unless ( $args{'install-base'} ) {
        my $dir = tempdir( $args{name} . '-XXXXXX', DIR => File::Spec->tmpdir );
        $args{'install-base'} = File::Spec->catfile( $dir, $args{name} );

        print $log
          "no default install-base, will set it to $args{'install-base'}\n";
    }

    open my $fh, '>', '__install_base'
      or die "can't write to __install_base: $!";
    print $fh $args{'install-base'};
    close $fh;

    unless ( $args{perl} && -e $args{perl} ) {
        if ( ( grep { $_ eq 'perl' } @$order )
            && !$args{skip}{perl} )
        {
            $args{perl} =
              File::Spec->catfile( $args{'install-base'}, 'bin', 'perl' );
        }
    }

    {
        no warnings 'uninitialized';
        $ENV{DYLD_LIBRARY_PATH} =
          File::Spec->catfile( $args{'install-base'}, 'lib' ) . ':'
          . $ENV{DYLD_LIBRARY_PATH};
        $ENV{LD_LIBRARY_PATH} =
          File::Spec->catfile( $args{'install-base'}, 'lib' ) . ':'
          . $ENV{LD_LIBRARY_PATH};
        $ENV{PERL5LIB} =
          File::Spec->catfile( $args{'install-base'}, 'lib', 'perl5',
            'site_perl' )
          . ':'
          . File::Spec->catfile( $args{'install-base'}, 'lib', 'perl5' ) . ':'
          . $ENV{PERL5LIB};
        $ENV{PATH} =
            File::Spec->catfile( $args{'install-base'}, 'bin' ) . ':'
          . File::Spec->catfile( $args{'install-base'}, 'sbin' ) . ':'
          . $ENV{PATH};
        $ENV{PERL_MM_USE_DEFAULT} = 1;
    }

    mkdir $args{'install-base'} unless -e $args{'install-base'};

    mkdir File::Spec->catfile( $args{'install-base'},       'etc' )
      unless -e File::Spec->catfile( $args{'install-base'}, 'etc' );
    mkdir File::Spec->catfile( $args{'install-base'},       'tools' )
      unless -e File::Spec->catfile( $args{'install-base'}, 'tools' );

    for ( 'shipwright-script-wrapper', 'shipwright-perl-wrapper' ) {
        copy( File::Spec->catfile( 'etc', $_ ),
            File::Spec->catfile( $args{'install-base'}, 'etc', $_ ) );
    }

    for ( 'shipwright-utility', 'shipwright-source-bash',
        'shipwright-source-tcsh' )
    {
        copy( File::Spec->catfile( 'etc', $_ ),
            File::Spec->catfile( $args{'install-base'}, 'tools', $_ ) );
    }

    chmod 0755,
      File::Spec->catfile( $args{'install-base'}, 'tools',
        'shipwright-utility' );

    for my $dist (@$order) {

        # $flags->{$dist} is undef means 'default', will be installed
        next
          if $flags->{$dist} && !grep { $args{flags}{$_} } @{ $flags->{$dist} };

        unless ( $args{skip}{$dist} ) {
            install($dist);
        }
        chdir $build_base;
    }

    mkdir File::Spec->catfile( $args{'install-base'},       'bin' )
      unless -e File::Spec->catfile( $args{'install-base'}, 'bin' );

    wrap_bin();
    print "install finished, the dists are at $args{'install-base'}\n";
    print $log "install finished, the dists are at $args{'install-base'}\n";
}

sub install {
    my $dir = shift;

    print "start to build and install $dir\n";

    if ( -e File::Spec->catfile( 'scripts', $dir, 'build.pl' ) ) {
        print $log "found build.pl for $dir, will install $dir using that\n";
        my $cmd = join ' ', $args{perl} || $^X,
          File::Spec->catfile( 'scripts', $dir, 'build.pl' ),
          '--source'       => File::Spec->catfile( 'dists', $dir ),
          '--install-base' => $args{'install-base'},
          '--flags' => join( ',', keys %{ $args{flags} } ),
          $args{'skip-test'} ? '--skip-test' : (),
          $args{'force'} ? '--force' : (), $args{'clean'} ? '--clean' : ();
        if ( system($cmd) ) {
            print $log "build $dir with failure: $!\n";
            die "build $dir with failure: $!\n";
        }
    }
    else {
        my $cmds = cmds( File::Spec->catfile( 'scripts', $dir, 'build' ) );

        chdir File::Spec->catfile( 'dists', $dir );

        for (@$cmds) {
            my ( $type, $cmd ) = @$_;
            next if $type eq 'clean';

            if ( $args{'skip-test'} && $type eq 'test' ) {
                print $log "skip build $type part in $dir\n";
                next;
            }

            print $log "build $type part in $dir with cmd: $cmd\n";

            print "we'll run the cmd: $cmd\n";
            if ( system($cmd) ) {
                print $log "build $dir $type part with failure: $!\n";
                if ( $args{force} && $type eq 'test' ) {
                    print $log
"although tests failed, will install anyway since we have force arg\n";
                }
                else {
                    die "build $dir $type part with failure: $!\n";
                }
            }
            else {
                print $log "build $dir $type part with success!\n";
            }
        }
    }

    print $log "build $dir with success!\n";
    print '=' x 80, "\n";
}

sub wrap_bin {
    my $self = shift;

    my %seen;

    my $sub = sub {
        my $file = $_;
        return unless $file and -f $file;
        return if $seen{$File::Find::name}++;
        my $dir = ( File::Spec->splitdir($File::Find::dir) )[-1];
        mkdir File::Spec->catfile( $args{'install-base'}, "$dir-wrapped" )
          unless -d File::Spec->catfile( $args{'install-base'},
            "$dir-wrapped" );

        my $type;
        if ( -T $file ) {
            open my $fh, '<', $file or die "can't open $file: $!";
            my $shebang = <$fh>;
            if (
                $shebang =~ m{
\Q$args{'install-base'}\E(?:/|\\)(?:s?bin|libexec)(?:/|\\)(\w+)
|\benv\s+(\w+)
}x
              )
            {
                $type = $1 || $2;
            }
        }

        move( $file =>
              File::Spec->catfile( $args{'install-base'}, "$dir-wrapped" ) )
          or die $!;

    # if we have this $type(e.g. perl) installed and have that specific wrapper,
    # then link to it, else link to the normal one
        if (   $type
            && grep( { $_ eq $type } @$order )
            && !$args{skip}{$type}
            && -e File::Spec->catfile( '..', 'etc', "shipwright-$type-wrapper" )
          )
        {
            symlink File::Spec->catfile( '..', 'etc',
                "shipwright-$type-wrapper" ) => $file
              or die $!;
        }
        else {

            symlink File::Spec->catfile( '..', 'etc',
                'shipwright-script-wrapper' ) => $file
              or die $!;
        }
        chmod 0755, $file;
    };

    my @dirs =
      grep { -d $_ }
      map { File::Spec->catfile( $args{'install-base'}, $_ ) }
      qw/bin sbin libexec/;
    find( $sub, @dirs ) if @dirs;

}

sub substitute {
    my $text = shift;
    return unless $text;

    my $install_base = $args{'install-base'};
    $text =~ s/%%INSTALL_BASE%%/$install_base/g;

    my $perl;

    if ( $args{perl} && -e $args{perl} ) {
        $perl = $args{perl};
    }
    else {
        $perl = $^X;
    }

    my $perl_archname = `$perl -MConfig -e 'print \$Config{archname}'`;
    $text =~ s/%%PERL%%/$perl/g;
    $text =~ s/%%PERL_ARCHNAME%%/$perl_archname/g;

    return $text;
}

sub test {

    my $cmds = cmds( File::Spec->catfile( 't', 'test' ) );

    for (@$cmds) {
        my ( $type, $cmd ) = @$_;
        print $log "run tests $type part with cmd: $cmd\n";
        if ( system($cmd ) ) {
            die "something wrong when execute $cmd: $?";
        }
        else {
            print $log "run test $type part with success\n";
        }
    }
    print $log "run tests with success\n";
}

sub cmds {
    my $file = shift;

    my @cmds;

    {
        open my $fh, '<', $file or die $!;
        @cmds = <$fh>;
        close $fh;
        chomp @cmds;
        @cmds = map { substitute($_) } @cmds;
    }

    my $return = [];
    for (@cmds) {
        my ( $type, $cmd );
        next unless /\S/;

        if (/^(\S+):\s*(.*)/) {
            $type = $1;
            $cmd  = $2;
        }
        else {
            $type = '';
            $cmd  = $_;
        }
        push @$return, [ $type, $cmd ];
    }

    return $return;
}

sub clean {
    my $dir = shift;

    my $cmd;

    if ( -e File::Spec->catfile( 'scripts', $dir, 'build.pl' ) ) {
        print $log "found build.pl for $dir, will clean $dir using that\n";
        $cmd = join ' ', $args{perl} || $^X,
          File::Spec->catfile( 'scripts', $dir, 'build.pl' ),
          '--source' => File::Spec->catfile( 'dists', $dir ),
          '--install-base' => $args{'install-base'},
          '--flags' => join( ',', keys %{ $args{flags} } ),
          '--clean';
    }
    else {
        my $cmds = cmds( File::Spec->catfile( 'scripts', $dir, 'build' ) );

        chdir File::Spec->catfile( 'dists', $dir );

        for (@$cmds) {
            my ( $type, $c ) = @$_;
            if ( $type eq 'clean' ) {
                $cmd = $c;
                last;
            }
        }
    }

    if ( system($cmd) ) {
        print $log "clean $dir with failure: $!\n";
    }
    else {
        print $log "clean $dir with success $!\n";
    }

    print '=' x 80, "\n";
}

sub get_install_base {
    if ( open my $fh, '<', '__install_base' ) {
        my $install_base = <$fh>;
        close $fh;
        chomp $install_base;
        return $install_base;
    }
    else {
        warn
"can't find install-base automatically, you need to specify it manually.\n";
    }

}
