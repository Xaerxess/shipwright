#!/usr/bin/env perl 
use strict;
use warnings;

use Getopt::Long;
use File::Spec::Functions qw/catfile catdir/;
use Cwd;
use Carp;

my %args;
GetOptions( \%args, 'install-links=s', 'switch=s', 'help' );

my $USAGE = <<'END'
run: ./tools/shipwright-utility --install-links /usr/local
run: ./tools/shipwright-utility --switch ubuntu

options: 

help: print this usage

install-links: link files in bin, sbin, or libexec to other places
    e.g. --install-links /usr/local

switch: switch to the lib below as/$name
    e.g. --switch ubuntu

END
  ;

if ( $args{'help'} ) {
    print $USAGE;
}
elsif ( $args{'install-links'} ) {
    my $cwd = getcwd();

    for my $dir (qw/bin sbin libexec/) {
        next unless -e $dir;
        my $dh;
        opendir $dh, $dir or confess $!;

        mkdir catfile( $args{'install-links'},       $dir )
          unless -e catfile( $args{'install-links'}, $dir );
        my @files = readdir $dh;
        for (@files) {
            next if $_ eq '.' || $_ eq '..';
            symlink catfile( $cwd, $dir, $_ ),
              catfile( $args{'install-links'}, $dir, $_ ) or confess
                  $!;
        }
    }
}
elsif ( $args{'switch'} ) {
    my $name = $args{'switch'};
    my $as_dir = catdir( 'as', $name );
    if ( -e $as_dir ) {
        for my $r ( qw/bin sbin lib/ ) {
            my $dir = catdir( $as_dir, $r );
            next unless -e $dir;

            if ( $r =~ /bin/ ) {
                unlink "$r-wrapped";
                symlink $dir, "$r-wrapped";
            }
            else {
                unlink $r;
                symlink $dir, $r;
            }
        }

        # in usr dir
        for my $r ( qw/bin sbin/ ) {
            my $dir = catdir( $as_dir, 'usr', $r );
            next unless -e $dir;
            unlink catfile( 'usr', "$r-wrapped" );
            symlink catdir( '..', $dir ), catfile( 'usr', "$r-wrapped" );
        }

        print "switched to $name with success.\n";

    }
    else {
        print "no switch name $name exists\n";
    }
}
